managed implementation in class zcl_bp_i_dxtravel unique;
strict ( 2 );

define behavior for ZI_DXTRAVEL //alias <alias_name>

persistent table /dmo/travel_m
with additional save
lock master
authorization master ( instance )
etag master LastChangedAt
early numbering
{
  create ( authorization : global );
  update;
  delete;
  field ( readonly ) TravelId, CreatedBy, CreatedAt, LastChangedBy, LastChangedAt;
  field ( mandatory ) AgencyId, CustomerId, BeginDate, EndDate, OverallStatus, BookingFee, CurrencyCode;
  association _Booking { create ( features : instance ); }
  action ( features : instance ) AcceptTravel result [1] $self;
  action ( features : instance ) RejectTravel result [1] $self;
  factory action CopyTravel [1];

  internal action reCalPrice;

  validation validateCustomer on save { create; field CustomerId; }
  validation validateDates on save { create; field BeginDate, EndDate; }
  validation validateStatus on save { create; field OverallStatus; }
  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateBookingFee on save { create; field BookingFee; }

  determination calTotalPrice on modify {  create;  field BookingFee, CurrencyCode; }

  mapping for /dmo/travel_m
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZI_DXBOOKING //alias <alias_name>
implementation in class zcl_bp_i_dxbooking unique
persistent table /dmo/booking_m
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, LastChangedAt;
  field ( mandatory ) CarrierId, ConnectionId, FlightDate, BookingStatus;
  field ( mandatory : create, readonly : update ) BookingDate, CustomerId;
  association _Travel;
  association _Booksupp { create ( features : instance ); }

  validation validateStatus on save { create; field BookingStatus; }
  validation validateCustomer on save { create; field CustomerId; }
  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateConnection on save { create; field CarrierId, ConnectionId, FlightDate; }
  validation validateFlightPrice on save { create; field FlightPrice; }
  determination calTotalPrice on modify {  create;  field FlightPrice, CurrencyCode; }

  mapping for /dmo/booking_m
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }
}

define behavior for ZDX_BOOKSUPP //alias <alias_name>
implementation in class zcl_bp_i_dxbooksupp unique
persistent table /dmo/booksuppl_m
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
early numbering
{
  update;
  delete;
  field ( readonly ) TravelId, BookingId, BookingSupplementId, LastChangedAt;
  field ( mandatory ) Price, SupplementId;
  association _Travel;
  association _Booking;

  validation validateCurrencyCode on save { create; field CurrencyCode; }
  validation validateSupplement on save { create; field SupplementId; }
  validation validatePrice on save { create; field price; }
  determination calTotalPrice on modify {  create;  field Price, CurrencyCode; }

  mapping for /dmo/booksuppl_m
    {
      TravelId            = travel_id;
      BookingId           = booking_id;
      BookingSupplementId = booking_supplement_id;
      SupplementId        = supplement_id;
      Price               = price;
      CurrencyCode        = currency_code;
      LastChangedAt       = last_changed_at;
    }
}